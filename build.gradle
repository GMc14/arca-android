// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'java'
apply plugin: 'maven'

dependencies {
    compile project(":arca-app")
    compile project(":arca-core")
}

uploadArchives {
    repositories.mavenDeployer {
        pom.whenConfigured {
            pom -> pom.packaging = "pom";
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

allprojects {
    apply plugin: 'maven'

    group = 'io.pivotal'
    version = '1.0-beta-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    uploadArchives {
        repositories.mavenDeployer {

            def userName = System.getenv('MAVEN_USER')
            def password = System.getenv('MAVEN_PASS')

            repository(url: 'https://maven.xtremelabs.com/artifactory/libs-snapshot-local') {
                authentication(userName: userName, password: password)
            }

            pom.project {
                name 'Arca Android'
                description 'A comprehensive framework for caching and displaying data.'
                url 'https://github.com/cfmobile/arca-android'

                scm {
                    url 'git@github.com:cfmobile/arca-android.git'
                    connection 'scm:git:git@github.com:cfmobile/arca-android.git'
                    developerConnection 'scm:git:git@github.com:cfmobile/arca-android.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jwinters'
                        name 'Joshua Winters'
                        email 'jwinters@gopivotal.com'
                    }
                }
            }
        }
    }
}