apply plugin: 'java'
apply plugin: 'maven'

dependencies {
    compile project(":arca-core:arca-broadcaster")
    compile project(":arca-core:arca-provider")
    compile project(":arca-core:arca-service")
    compile project(":arca-core:arca-threading")
    compile project(":arca-core:arca-utils")
}

uploadArchives {
    repositories.mavenDeployer {
        pom.whenConfigured {
            pom -> pom.packaging = "pom";
        }
    }
}

subprojects { project ->
    apply plugin: 'android-library'

    android {
        compileSdkVersion 19
        buildToolsVersion "19.0.3"

        defaultConfig {
            minSdkVersion 9
            targetSdkVersion 19
            versionCode 1
            versionName "1.0"
        }

        sourceSets {
            androidTest.setRoot('src/test')
        }
    }

    task jarClasses(type: Jar, dependsOn: assembleRelease) {
        from fileTree(dir: 'build/classes/release')
    }

    task jarSources(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java
    }

    task jarJavadoc(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jarClasses
        archives jarSources
        archives jarJavadoc
    }
}